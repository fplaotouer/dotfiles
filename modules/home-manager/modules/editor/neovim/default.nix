{
  lib,
  config,
  pkgs,
  ...
}: let
  cfg = config.programs.neovim;
in {
  options = {
    programs.neovim = {
      lazyVim = lib.mkEnableOption "LazyVim";
    };
  };
  config = {
    xdg.configFile = lib.mkIf cfg.lazyVim {
      "nvim/lua" = {
        source = ./nvim/lua;
        recursive = true;
      };
      "nvim/after" = {
        source = ./nvim/after;
        recursive = true;
      };
    };

    programs.neovim = {
      # package = pkgs.neovim-nightly;
      vimAlias = true;
      extraLuaPackages = luaPkgs: with luaPkgs; [luautf8];
      extraPython3Packages = pyPkgs: with pyPkgs; [pynvim];
      extraPackages = lib.mkIf cfg.lazyVim (builtins.attrValues {
        inherit
          (pkgs)
          luajit
          tree-sitter
          shellcheck
          shfmt
          nil
          ruff
          pyright
          black
          sumneko-lua-language-server
          stylua
          ;
        inherit
          (pkgs.nodePackages)
          nodejs
          bash-language-server
          yaml-language-server
          ;
      });
      extraLuaConfig = lib.concatStringsSep "\n" [
        "-- Configurations generated by home-manager."
        (builtins.readFile ./nvim/lazy.lua)
      ];
    };
  };
}
