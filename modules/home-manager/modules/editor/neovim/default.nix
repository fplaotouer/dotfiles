{
  lib,
  config,
  pkgs,
  ...
}: let
  cfg = config.programs.neovim;
in {
  options = {
    programs.neovim = {
      lazyVim = lib.mkEnableOption "LazyVim";
    };
  };
  config = {
    xdg.configFile = lib.mkIf cfg.lazyVim {
      "nvim/lua" = {
        source = ./nvim/lua;
        recursive = true;
      };
      "nvim/after" = {
        source = ./nvim/after;
        recursive = true;
      };
    };

    programs.neovim = {
      package = pkgs.neovim;
      vimAlias = true;
      extraLuaPackages = luaPkgs: with luaPkgs; [luautf8 luarocks magick];
      extraPython3Packages = pyPkgs: with pyPkgs; [pynvim];
      extraPackages = with pkgs; [luajit tree-sitter nodePackages.nodejs statix];
      extraLuaConfig = lib.optionalString cfg.lazyVim (lib.concatStringsSep "\n" [
        "-- Configurations generated by home-manager."
        (builtins.readFile ./nvim/lazy_nvim.lua)
      ]);
    };
  };
}
